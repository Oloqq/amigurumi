program = { SOI ~ (round | comment | meta | NEWLINE)+ ~ EOI }

meta    = { "@" ~ metakey ~ "=" ~ NUMBER ~ NEWLINE }
metakey = { "centroids" }

round       =  { roundspec? ~ ":" ~ stitches ~ round_end ~ (comment | NEWLINE) }
roundspec   = _{
    round_range
  | round_index
  | NUMBER
}
round_index = @{ R ~ NUMBER }
round_range = @{ R ~ NUMBER ~ "-" ~ R ~ NUMBER }
round_end   =  { "(" ~ (NUMBER | PLACEHOLDER) ~ ")" }

stitches   = { numstitch ~ ("," ~ numstitch)* }
numstitch  = { (NUMBER? ~ stitch) | repetition }
stitch     = { "sc" | "inc" | "dec" }
repetition = { "[" ~ stitches ~ "]" ~ "x" ~ #times = NUMBER }

comment     = _{ "#" ~ not_newline* ~ NEWLINE }
not_newline = _{
    !(NEWLINE) ~ ANY
}

PLACEHOLDER =  { "_" }
R           =  { "R" }
ALPHA       =  { 'a'..'z' | 'A'..'Z' }
NUMBER      = @{ NONZERO ~ DIGIT* }
NONZERO     = _{ '1'..'9' }
DIGIT       = _{ '0'..'9' }
WHITESPACE  = _{ " " }
