program = { SOI ~ (round | comment | meta | control | NEWLINE)+ ~ EOI }

meta    = { "@" ~ IDENT ~ "=" ~ IDENT ~ LINEEND }

round       =  { roundspec? ~ ":" ~ stitches ~ round_end ~ LINEEND } // TODO make round_end optional
roundspec   = {
    round_range
  | round_index
  | NUMBER
}
round_index = @{ R ~ NUMBER }
round_range = @{ R ~ NUMBER ~ "-" ~ R ~ NUMBER }
round_end   =  { "(" ~ (NUMBER | PLACEHOLDER) ~ ")" }

stitches   = { stitch_sequence ~ ("," ~ stitch_sequence)* }
stitch_sequence  = {
      (NUMBER ~ KW_STITCH)
    | KW_STITCH
    | interstitchable_operations
    | repetition }
repetition = { "[" ~ stitches ~ "]" ~ "x" ~ #times = NUMBER }

control = { operation_sequence ~ LINEEND }
operation_sequence = { operation ~ ("," ~ operation)* }
arg_int = { "(" ~ NUMBER ~ ")" }
operation = {
      (MR ~ arg_int)
    |  FO
}
interstitchable_operations = {
    "TODO"
}
MR = { "MR" }
FO = { "FO" }

comment     = _{ "#" ~ not_newline* ~ NEWLINE }
not_newline = _{
    !(NEWLINE) ~ ANY
}

LINEEND     =  { NEWLINE | comment | EOI }
KW_STITCH   =  { "sc" | "inc" | "dec" }
PLACEHOLDER =  { "_" }
R           =  { "R" }
ALPHA       =  { 'a'..'z' | 'A'..'Z' }
IDENT       = @{ (ALPHA | DIGIT | "_")+ }
NUMBER      = @{ NONZERO ~ DIGIT* }
NONZERO     = _{ '1'..'9' }
DIGIT       = _{ '0'..'9' }
WHITESPACE  = _{ " " }
