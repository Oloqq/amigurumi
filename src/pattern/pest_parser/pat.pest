program = { SOI ~ (round | comment | meta | control | NEWLINE)+ ~ EOI }

meta    = { "@" ~ metakey ~ "=" ~ NUMBER ~ NEWLINE }
metakey = { "centroids" }

round       =  { roundspec? ~ ":" ~ stitches ~ round_end ~ LINEEND }
roundspec   = {
    round_range
  | round_index
  | NUMBER
}
round_index = @{ R ~ NUMBER }
round_range = @{ R ~ NUMBER ~ "-" ~ R ~ NUMBER }
round_end   =  { "(" ~ (NUMBER | PLACEHOLDER) ~ ")" }

stitches   = { stitch_sequence ~ ("," ~ stitch_sequence)* }
stitch_sequence  = { (NUMBER ~ KW_STITCH) | KW_STITCH | repetition }
repetition = { "[" ~ stitches ~ "]" ~ "x" ~ #times = NUMBER }

control = { KW_CONTROL ~ LINEEND }

comment     = _{ "#" ~ not_newline* ~ NEWLINE }
not_newline = _{
    !(NEWLINE) ~ ANY
}

LINEEND     =  { NEWLINE | comment | EOI }
KW_STITCH    =  { "sc" | "inc" | "dec" }
KW_CONTROL  =  { "FO" }
PLACEHOLDER =  { "_" }
R           =  { "R" }
ALPHA       =  { 'a'..'z' | 'A'..'Z' }
NUMBER      = @{ NONZERO ~ DIGIT* }
NONZERO     = _{ '1'..'9' }
DIGIT       = _{ '0'..'9' }
WHITESPACE  = _{ " " }
